plugins {
    id "groovy"
    id "java"
    id "maven-publish"
    id "com.jfrog.artifactory" version "3.0.1"
}

group = 'se.mtm.gradle'

project.ext {
    jerseyVersion = '2.28'
}

dependencies {
    compile gradleApi()

    compile 'org.glassfish.jersey.core:jersey-client:' + jerseyVersion
    compile 'org.glassfish.jersey.connectors:jersey-grizzly-connector:' + jerseyVersion
	compile 'org.glassfish.jersey.inject:jersey-hk2:' + jerseyVersion
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-io:commons-io:2.4'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.+"
}


repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url "http://artifactory.mtm.se:8081/artifactory/repo"
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "${artifactory_upload_repo}"
            username = "${mavenCredentialUsr}"
            password = "${mavenCredentialPwd}"
            maven = true
        }
    }
    resolve {
        repository {
            repoKey = "${artifactory_resolve_repo}"
            maven = true
        }
    }
}

publishing {
    publications {
        mavenDependencyList(MavenPublication) {
            groupId group
            artifactId "gradle-artifactory-rpm-plugin"
            version "${version}"

            from components.java
            fixDependencyScope(pom)
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.mavenDependencyList)
}

artifactoryPublish.dependsOn(build)

test {
    testLogging.showStandardStreams = true
}

void fixDependencyScope(pom) {
    pom.withXml {
        asNode().dependencies.'*'.findAll() {
            it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                dep.name == it.artifactId.text()
            }
        }.each() {
            it.scope*.value = 'compile'
        }
    }
}
